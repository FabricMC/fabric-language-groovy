plugins {
	id 'groovy'
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "6.1.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "fabric-loom" version "0.5-SNAPSHOT"
}

archivesBaseName = modid

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mod_version
def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
}

configurations {
	shadow
	compile.extendsFrom shadow
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	shadow "org.codehaus.groovy:groovy-all:${groovy_version}"
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

shadowJar {
	classifier = "fat"
	configurations = [project.configurations.shadow]
	exclude("META-INF")
}

tasks.build.dependsOn shadowJar

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

curseforge {
	if (ENV.CURSEFORGE_API_KEY) {
		apiKey = ENV.CURSEFORGE_API_KEY
	}

	project {
		id = "336737"
		changelog = "A changelog can be found at https://github.com/FabricMC/fabric-language-groovy"
		releaseType = "release"
		addGameVersion "1.14.4"
		addGameVersion "1.15.2"
		addGameVersion "1.16"
		addGameVersion "1.16.4"
		addGameVersion "1.17"
		addGameVersion "Fabric"

		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}-fat.jar")) {
			displayName = "Fabric Language Groovy $version"
		}

		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}

	options {
		forgeGradleIntegration = false
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId project.maven_group
			artifactId project.archivesBaseName
			version project.version

			artifact(remapJar) {
				builtBy remapJar
			}
		}
	}
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/net/fabricmc/fabric-language-groovy/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion
project.tasks.curseforge.mustRunAfter checkVersion